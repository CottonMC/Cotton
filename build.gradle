plugins {
    id "com.jfrog.artifactory" version "4.9.0"
    id "maven-publish"
    id "fabric-loom" version "0.4-SNAPSHOT"
}

def minecraftVersion = "1.16.3"
def yarnMappings = "1.16.3+build.47:v2"
def loaderVersion = "0.10.6+build.214"

archivesBaseName = "cotton"
version = "1.1.0"
group = "io.github.cottonmc.cotton"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    maven {
        name = "Fabric Maven"
        url = "http://maven.fabricmc.net"
    }
    maven {
        name = "Cotton"
        url = "https://server.bbkr.space/artifactory/libs-release/"
    }
    maven {
        name = "Cotton (snapshots)"
        url = "https://server.bbkr.space/artifactory/libs-snapshot/"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:$minecraftVersion"
    mappings "net.fabricmc:yarn:$yarnMappings"
    modImplementation "net.fabricmc:fabric-loader:$loaderVersion"
    modImplementation "net.fabricmc.fabric-api:fabric-api:0.25.0+build.415-1.16"

    // `modApi`s below are for the dev env
    def modules = ["player-events", "logging", "config", "datapack", "commons", "cauldron"]
    modules.each { module ->
        modApi ("io.github.cottonmc.cotton:cotton-$module:+") { transitive = false }
        include "io.github.cottonmc.cotton:cotton-$module:+"
    }
    modApi ("io.github.cottonmc:Jankson-Fabric:3.0.1+j1.2.0") { transitive = false }
    modImplementation "io.github.cottonmc:LibCD:3.0.3+1.16.3"
    modImplementation "io.github.cottonmc:LibDP:1.1.0+1.16.2"
    include "io.github.cottonmc:Jankson-Fabric:3.0.1+j1.2.0"

    compileOnly ("com.google.code.findbugs:jsr305:3.0.2") { transitive = false }
}


processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

jar {
    from rootProject.files("LICENSE")
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact ("${project.buildDir.absolutePath}/libs/${archivesBaseName}-${project.version}.jar") {
                classifier null
                builtBy remapJar
            }

            artifact ("${project.buildDir.absolutePath}/libs/${archivesBaseName}-${project.version}-dev.jar") {
                classifier "dev"
                builtBy remapJar
            }

            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }
}

//Publishing details
if (rootProject.file("private.gradle").exists()) {
    apply from: "private.gradle"
}

artifactory {
    if (rootProject.hasProperty("artifactoryUsername")) {
        contextUrl = "http://server.bbkr.space:8081/artifactory/"
        publish {
            repository {
                if (version.contains("SNAPSHOT")) {
                    repoKey = "libs-snapshot"
                } else {
                    repoKey = "libs-release"
                }

                username = artifactoryUsername
                password = artifactoryPassword
            }
            defaults {
                publications("maven")

                publishArtifacts = true
                publishPom = true
            }
        }
    } else {
        println "Cannot configure artifactory; please define ext.artifactoryUsername and ext.artifactoryPassword before running artifactoryPublish"
    }
}
