buildscript {
	repositories {
		mavenCentral()
		jcenter()
		maven {
			name = 'Fabric'
			url = 'http://maven.modmuss50.me/'
		}
		maven {
			url "https://plugins.gradle.org/m2/"
		}
        maven{
            url = "http://server.bbkr.space:8081/artifactory/libs-snapshot/"
        }

	}
	dependencies {
		classpath "net.fabricmc:fabric-loom:0.2.1-SNAPSHOT"
		classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.0'
	}
}

plugins {
	id 'java'
	id 'eclipse'
	id 'idea'
}

apply plugin: 'maven-publish'	//to make jitpack execute the build task instead of the jar task
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: net.fabricmc.loom.LoomGradlePlugin

sourceCompatibility = 1.8
targetCompatibility = 1.8

archivesBaseName = "cotton"
version = "18w50a-0.0.2"

repositories {
    maven {
    	//jankson
        url "http://repo.elytradev.com/"
    }
}

dependencies {
	minecraft "com.mojang:minecraft:18w50a"
	mappings "net.fabricmc:yarn:18w50a.71"
	modCompile "net.fabricmc:fabric-loader:0.3.0.75"
	modCompile "net.fabricmc:fabric:0.1.2.64"
	
	modCompile "io.github.prospector.silk:SilkAPI:1.0.0-25"

	//jankson, the json parser we are using to allow comments
	modCompile "blue.endless:jankson:1.0.0-7"
	shadow "blue.endless:jankson:1.0.0-7"
}

publishing {
	publications {
		maven(MavenPublication) {
			groupId = 'io.github.cottonmc'
			artifactId = 'cotton'
			
			/* START TERRIBLE, HORRIBLE, AWFUL, VERY BAD HACK. Removes entire minecraft dependencies from POM */
			pom.withXml {
				def projectNode = asNode();
				def deps = projectNode.get("dependencies");
				projectNode.remove(deps);
			}
			/* END TERRIBLE, HORRIBLE, AWFUL, VERY BAD HACK */
			
			from components.java
			//Below is a slightly less noxious, different hack so that we pick up the correct reobfShadowJar.
			artifacts = [ "build/libs/cotton-"+version+".jar" ]
		}
	}
}


shadowJar {
	relocate 'blue.endless.jankson', 'io.github.cottonmc.repackage.blue.endless.jankson'
	configurations = [project.configurations.shadow]
	classifier ''
}

tasks.remapJar.dependsOn.remove Jar
tasks.remapJar.dependsOn shadowJar
tasks.publish.dependsOn build
tasks.publishToMavenLocal.dependsOn build